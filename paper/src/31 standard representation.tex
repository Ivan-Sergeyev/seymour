\section{Standard Representations}

The \emph{standard representation} \cite{Oxley2011,Truemper2016} of a vector matroid is the following structure:
\begin{leancode}
structure StandardRepr (α R : Type*)
    [DecidableEq α] where
  X : Set α
  Y : Set α
  hXY : X ⫗ Y
  B : Matrix X Y R
  decmemX : ∀ a, Decidable (a ∈ X)
  decmemY : ∀ a, Decidable (a ∈ Y)
\end{leancode}
In essence, this is a wrapper for the standard representation matrix $B$ indexed by disjoint sets $X$ and $Y$, bundled together with the membership decidability for $X$ and $Y$. The standard representation matrix $B$ corresponds to the full representation matrix $\begin{NiceArray}{cc}[hvlines] \mathbbm{1} & B \\ \end{NiceArray}$ with the conversion implemented as
\begin{leancode}
def StandardRepr.toFull {α R : Type*}
    [DecidableEq α] [Zero R] [One R]
    (S : StandardRepr α R) :
    Matrix S.X (S.X ∪ S.Y).Elem R :=
  ((Matrix.fromCols 1 S.B) · ∘ Subtype.toSum)
\end{leancode}
Thus, the vector matroid given by its standard representation is constructed as follows:
\begin{leancode}
def StandardRepr.toMatroid {α R : Type*}
    [DecidableEq α] [DivisionRing R]
    (S : StandardRepr α R) :
    Matroid α :=
  S.toFull.toMatroid
\end{leancode}
In this matroid, the ground set is $X \cup Y$, and a set $I \subseteq X \cup Y$ is independent if the columns of
$\begin{NiceArray}{cc}[hvlines] \mathbbm{1} & B \\ \end{NiceArray}$
indexed by $I$ are linearly independent over $R$.

Below are several results we prove about standard representations, which are either used in the proof of regularity of 1-, 2-, and 3-sums, or could be useful for downstream projects.

First, we show that if the row index set $X$ of a standard representation is finite, then $X$ is a base in the resulting matroid:
\begin{leancode}
lemma StandardRepr.toMatroid_isBase_X
    {α R : Type*} [DecidableEq α] [Field R]
    (S : StandardRepr α R) [Fintype S.X] :
    S.toMatroid.IsBase S.X
\end{leancode}
This lemma characterizes what sets can serve as row index sets of standard representations and motivates the corresponding hypotheses in the code snippets below.

Next, we prove that a full representation of a vector matroid can be transformed into a standard representation of the same matroid, with a given base as the row index set:
\begin{leancode}
lemma Matrix.exists_standardRepr_isBase
    {α R : Type*} [DecidableEq α] [DivisionRing R]
    {X Y G : Set α} (A : Matrix X Y R)
    (hAG : A.toMatroid.IsBase G) :
    ∃ S : StandardRepr α R,
      S.X = G ∧
      S.toMatroid = A.toMatroid
\end{leancode}
In classical literature on matroid theory \cite{Oxley2011,Truemper2016}, this follows by simply performing a sequence of elementary row operations akin to Gaussian elimination. Our formal proof used a different approach, utilizing Mathlib's results about bases and linear independence. First, we showed that the columns indexed by \texttt{G} form a basis of the module generated by all columns of \texttt{A}. Then we proved that performing a basis exchange yields the correct standard representation matrix.

We also prove an analog of the above lemma that additionally preserves total unimodularity of the representation matrix:
% Full name: Matrix.exists_standardRepr_isBase_isTotallyUnimodular
% Martin: footnote with the full name?
% Ivan: the same issue (Lean names too long) appears in many places; I would try to come up with a general solution
% Martin: But only two of them are so long that they don't fit into a line.
\begin{leancode}
lemma Matrix.exists_standardRepr_isBase_isTU
    {α R : Type*} [DecidableEq α] [Field R]
    {X Y G : Set α} [Fintype G]
    (A : Matrix X Y R)
    (hAG : A.toMatroid.IsBase G)
    (hA : A.IsTotallyUnimodular) :
    ∃ S : StandardRepr α R,
      S.X = G ∧
      S.toMatroid = A.toMatroid ∧
      S.B.IsTotallyUnimodular
\end{leancode}
Classical literature \cite{Oxley2011,Truemper2016} observes that elementary row operations preserve total unimodularity and then simply refers to the proof of the previous lemma. Unfortunately, we could not take advantage of such a reduction, as it would be hard to verify that total unimodularity is preserved in our prior approach. Thus, we implemented an inductive proof essentially following the ideas of \cite{Oxley2011,Truemper2016}. Note that this lemma takes stronger assumptions than the previous one, namely \texttt{G} has to be finite and multiplication in \texttt{R} has to commute.

Another result we prove is that two standard representations of the same vector matroid over $\mathbb{Z}_{2}$ with the same finite row index set must be identical:
\begin{leancode}
lemma ext_standardRepr_of_same_matroid_same_X
    {α : Type*} [DecidableEq α]
    {S₁ S₂ : StandardRepr α Z2} [Fintype S₁.X]
    (hSS : S₁.toMatroid = S₂.toMatroid)
    (hXX : S₁.X = S₂.X) :
    S₁ = S₂
\end{leancode}
Although this particular lemma is not employed later in our project, it captures an important result that a binary matroid has an essentially unique standard representation \cite{Oxley2011,Truemper2016}. Nevertheless, we make use of a very similar result:
\begin{leancode}
lemma support_eq_support_of_same_matroid_same_X
    {F₁ : Type u₁} {F₂ : Type u₂}
    {α : Type max u₁ u₂ v} [DecidableEq α]
    [DecidableEq F₁] [DecidableEq F₂]
    [Field F₁] [Field F₂]
    {S₁ : StandardRepr α F₁}
    {S₂ : StandardRepr α F₂}
    [Fintype S₂.X]
    (hSS : S₁.toMatroid = S₂.toMatroid)
    (hXX : S₁.X = S₂.X) :
    let hYY : S₁.Y = S₂.Y := sorry
    hXX ▸ hYY ▸ S₁.B.support = S₂.B.support
\end{leancode}
This states that two standard representations of a vector matroid with identical (finite) row index sets have the same support, i.e., the zeros in them appear on identical positions. Crucially, this holds for any two standard representations over any two fields (where equality is decidable), and we later use it for $\mathbb{Q}$ and $\mathbb{Z}_{2}$.
