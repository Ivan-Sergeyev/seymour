\subsection{Matroids}

Matroids have many equivalent definitions \cite{Oxley2011,Truemper2016,Bruhn2013}. In Mathlib, the structure \texttt{Matroid} captures the following mathematical definition via the \emph{base axioms} from \cite{Bruhn2013}: a \emph{matroid} is a pair $M = (E, \mathcal{B})$ where $E$ is a (potentially infinite) ground set and $\mathcal{B} \subseteq 2^{E}$ is a collection of sets such that:
\begin{enumerate}[label=(\roman*)]
    \item $\mathcal{B} \neq \emptyset$.
    \item\label{it:im_basis_exch} For all $B_{1}, B_{2} \in \mathcal{B}$ and all $b_{1} \in B_{1} \setminus B_{2}$, there exists $b_{2} \in B_{2} \setminus B_{1}$ such that $(B_{1} \setminus \{b_{1}\}) \cup \{b_{2}\} \in \mathcal{B}$.
    \item For all $X \subseteq E$ and $I \subseteq X$ such that $I \subseteq B_{1}$ for some $B_{1} \in \mathcal{B}$, there exists a maximal $Y \subseteq X$ such that $Y \subseteq B_{2}$ for some $B_{2} \in \mathcal{B}$.
\end{enumerate}
A set $B \in \mathcal{B}$ is called a \emph{base}, and \ref{it:im_basis_exch} is known as the \emph{base exchange property}. Additionally, if a set $I \subseteq E$ is a subset of any base, then $I$ is called \emph{independent}. The definition above generalizes the classical notion of matroids \cite{Oxley2011,Truemper2016}, which can only have finite ground sets. Mathlib implements matroids as follows:
\begin{leancode}
def Matroid.ExchangeProperty {α : Type*} 
    (P : Set α → Prop) : Prop :=
  ∀ X Y, P X → P Y → ∀ a ∈ X \ Y,
    ∃ b ∈ Y \ X, P (insert b (X \ {a}))
\end{leancode}
\begin{leancode}
def Maximal (P : α → Prop) (x : α) : Prop :=
  P x ∧ ∀ ⦃y⦄, P y → x ≤ y → y ≤ x
\end{leancode}
\begin{leancode}
def Matroid.ExistsMaximalSubsetProperty 
    {α : Type*} (P : Set α → Prop) 
    (X : Set α) : Prop :=
  ∀ I, P I → I ⊆ X → ∃ J, I ⊆ J 
    ∧ Maximal (fun K ↦ P K ∧ K ⊆ X) J
\end{leancode}
\begin{leancode}
structure Matroid (α : Type*) where
  (E : Set α)
  (IsBase : Set α → Prop)
  (Indep : Set α → Prop)
  (indep_iff' : ∀ ⦃I⦄, Indep I ↔ ∃ B,
    IsBase B ∧ I ⊆ B)
  (exists_isBase : ∃ B, IsBase B)
  (isBase_exchange : 
    Matroid.ExchangeProperty IsBase)
  (maximality : ∀ X, X ⊆ E →
    Matroid.ExistsMaximalSubsetProperty 
      Indep X)
  (subset_ground : ∀ B, IsBase B → B ⊆ E)
\end{leancode}

Additionally, Mathlib allows the user to construct matroids (potentially infinite) in terms of the \emph{independence axioms} using
\begin{leancode}
structure IndepMatroid (α : Type*) where
  (E : Set α)
  (Indep : Set α → Prop)
  (indep_empty : Indep ∅)
  (indep_subset : ∀ ⦃I J⦄,
    Indep J → I ⊆ J → Indep I)
  (indep_aug : ∀ ⦃I B⦄, Indep I → 
    ¬ Maximal Indep I →
    Maximal Indep B →
    ∃ x ∈ B \ I, Indep (insert x I))
  (indep_maximal : ∀ X, X ⊆ E → 
    Matroid.ExistsMaximalSubsetProperty
    Indep X)
  (subset_ground : ∀ I, Indep I → I ⊆ E)
\end{leancode}
One can then obtain \texttt{Matroid α} from \texttt{IndepMatroid α} via \texttt{IndepMatroid.matroid}. The independence axioms frequently appear in constructions and proofs in classical literature \cite{Oxley2011,Truemper2016}, and we use \texttt{IndepMatroid} to construct matroids in our library.

Though we generally work with infinite matroids, our final results require that the matroids have finite \emph{rank}. A finite-rank matroid is one that has a finite base, implemented in Mathlib as
\begin{leancode}
class RankFinite {α : Type*} (M : Matroid α) :
    Prop where
  exists_finite_isBase :
    ∃ B, M.IsBase B ∧ B.Finite
\end{leancode}

\begin{comment}
    If we really need to save space, I can imagine entirely leaving out implementation details of \texttt{Matroid} and \texttt{IndepMatroid} and only providing their signatures. This would move the focus towards how to use/work with matroids, especially in the context of our project, and omit how they are implemented in Mathlib.
\end{comment}
